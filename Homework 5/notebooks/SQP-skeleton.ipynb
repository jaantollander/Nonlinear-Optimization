{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "using Ipopt\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using Plots\n",
    "\n",
    "## Gradient and Hessian\n",
    "∇(f,x)  = ForwardDiff.gradient(f,x)\n",
    "∇²(f,x) = ForwardDiff.hessian(f,x)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## Problem data\n",
    "n = 2  # Number of variables\n",
    "m = 4  # Number of constraints (and dual variables)\n",
    "\n",
    "## Objective function, its gradient and Hessian\n",
    "f(x)   = 2x[1]^2 + 2x[2]^2 - 2(x[1]*x[2]) - 4x[1] - 6x[2]\n",
    "∇f(x)  = ∇(f,x)\n",
    "∇²f(x) = ∇²(f,x)\n",
    "\n",
    "## Constraint functions\n",
    "g₁(x) = x[1]^2 - x[2]     # ≦ 0\n",
    "g₂(x) = x[1] + 5x[2] - 5  # ≦ 0\n",
    "g₃(x) = -x[1]             # ≦ 0\n",
    "g₄(x) = -x[2]             # ≦ 0\n",
    "\n",
    "## Constraint function gradients and Hessians\n",
    "∇g₁(x)  = ∇(g₁,x)\n",
    "∇²g₁(x) = ∇²(g₁,x)       # Only g₁ has nonzero Hessian\n",
    "∇g₂(x)  = ∇(g₂,x)\n",
    "∇g₃(x)  = ∇(g₃,x)\n",
    "∇g₄(x)  = ∇(g₄,x)\n",
    "\n",
    "## Lagrangian function of the poblem\n",
    "L(x,u) = f(x) + u[1]*g₁(x) + u[2]*g₂(x) + u[3]*g₃(x) + u[4]*g₄(x)\n",
    "∇²L(x,u) = ∇²f(x) + u[1]*∇²g₁(x)  # g₂, g₃, and g₄ vanish as they are linear.\n",
    "\n",
    "## Initialize data\n",
    "μ  = 10           # Penalty parameter\n",
    "Δᵏ = 1            # Trust region\n",
    "k  = 1            # Iteration count\n",
    "N  = 10           # Max iterations\n",
    "ϵ  = 1e-6         # Tolerance\n",
    "xᵏ = [0.5; 0.5]   # Initial primal solutionm\n",
    "uᵏ = zeros(4)     # initial dual solution\n",
    "x  = zeros(2, N)  # Save trajectory of iteratation\n",
    "x[:,k] = xᵏ       # Set initial value\n",
    "\n",
    "## Main loop\n",
    "for k = 1:N-1\n",
    "    xᵏ = x[:,k]\n",
    "    ## Compute ∇f, ∇g, and ∇L²\n",
    "    ∇fᵏ  = ∇f(xᵏ)\n",
    "    ∇gᵏ  = [∇g₁(xᵏ), ∇g₂(xᵏ), ∇g₃(xᵏ), ∇g₄(xᵏ)]\n",
    "    ∇L²ᵏ = ∇²L(xᵏ,uᵏ)\n",
    "    gᵏ   = [g₁(xᵏ), g₂(xᵏ), g₃(xᵏ), g₄(xᵏ)]\n",
    "\n",
    "    ## Projected lagrangian subproblem (Direction search)\n",
    "    QP = Model(with_optimizer(Ipopt.Optimizer))\n",
    "    @variable(QP, d[1:n])\n",
    "    @variable(QP, y[1:m] >= 0)\n",
    "\n",
    "    #### TODO: Write the constraints for the trust region and the penalty in the objective \n",
    "    @objective(QP, Min,\n",
    "        ∇fᵏ' * d + \n",
    "        1/2 * (d' * (∇L²ᵏ * d)) +\n",
    "        μ * sum(y[i] for i in 1:m)\n",
    "    )\n",
    "    @constraint(QP, [j = 1:n], -Δᵏ ≤ d[j] ≤ Δᵏ)\n",
    "    @constraint(QP, [i = 1:m], y[i] ≥ gᵏ[i] + ∇gᵏ[i]' * d)\n",
    "\n",
    "    optimize!(QP)\n",
    "\n",
    "    dᵏ = value.(d)          # Obtain new direction\n",
    "    x[:,k+1] = xᵏ + dᵏ        # Update primal solution\n",
    "    #uᵏ = dual.(LinearIneq)  # Obtain optimal dual solution\n",
    "\n",
    "    ## Check stopping condition\n",
    "    if norm(dᵏ) < ϵ\n",
    "        x = x[:,1:k+1]\n",
    "        break\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## Plotting\n",
    "n  = 1000\n",
    "x1 = LinRange(-1,2,n)\n",
    "x2 = LinRange(-1,2,n)\n",
    "z  = [f([x1[i],x2[j]]) for j = 1:n, i = 1:n]\n",
    "\n",
    "## Contours of the objective\n",
    "contour(x1, x2, z,\n",
    "        levels  = [-11, -9, -7, -5, -3, -1],\n",
    "        clims = (-20,5),\n",
    "        clabels = true,\n",
    "        legend = false)\n",
    "## Plot the feasible region\n",
    "plot!(x1, x1.^2, fill = (10,0.2), color = 2)\n",
    "plot!(x1, 1 .- (1/5).*x1, fill = (0,0.2), color = 3,\n",
    "      xaxis = (\"x1\", (0,2)),\n",
    "      yaxis = (\"x2\", (0,2)),\n",
    "      aspect_ratio = :equal,\n",
    "      size = (800,800),\n",
    "      legend = false)\n",
    "## Plot the trejectory of iterations\n",
    "plot!(x[1,:], x[2,:], marker = :o, color = 1)\n",
    "\n",
    "## First order approximation at initial point of constraint 1\n",
    "display(plot!(x1, -0.25 .+ x1, color = 3, line = :dash))\n",
    "\n",
    "savefig(\"SQP_example.pdf\")"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.1.1",
   "language": "julia",
   "name": "julia-1.1"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.1.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
